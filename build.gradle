plugins {
	id 'org.springframework.boot' version '2.5.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.github.spotbugs' version '4.6.0'
	id 'org.owasp.dependencycheck' version '6.2.0'
}

group = 'uk.nhs.prm.deductions'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-activemq'
	implementation 'net.logstash.logback:logstash-logback-encoder:6.4'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.11.1'
	implementation 'com.fasterxml.woodstox:woodstox-core:5.1.0'
	implementation 'org.apache.activemq:activemq-broker:5.16.1'
	implementation 'org.apache.activemq:activemq-client:5.16.1'
	implementation 'com.sun.mail:javax.mail:1.6.2'
	implementation 'com.google.code.gson:gson:2.8.6'
	implementation 'com.github.spotbugs:spotbugs-annotations:3.1.3'
	implementation 'junit:junit:4.13.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.0'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation group: 'de.mkammerer.wiremock-junit5', name: 'wiremock-junit5', version: '1.1.0'
	testImplementation(platform('org.junit:junit-bom:5.7.2'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
	testImplementation "com.github.tomakehurst:wiremock-jre8:2.27.2"
}

configurations {
	compile.exclude module: 'activemq-broker'
}

//Without this task two jars are built, the additional "-plain.jar" is not needed
// for more details refer to: https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
	enabled = false
}

test {
	useJUnitPlatform()
}

task unitTest(type: Test) {
	useJUnitPlatform {
		excludeTags 'integration'
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform {
		excludeTags 'unit'
	}
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport

	violationRules {
		rule {
			limit {
				minimum = 0.7
			}
		}
	}
}

spotbugsMain {
	reports {
		html {
			enabled = true
			destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	reports {
		html {
			enabled = true
			destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

dependencyCheck {
	failBuildOnCVSS=7
	suppressionFile='./dependency-checks-suppression.xml'
}